plugins {
    id 'com.gradle.plugin-publish' version '0.10.0'
}

apply plugin: 'groovy'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile(group: 'kr.motd.maven', name: 'sphinx-maven-plugin', version: '2.3.1') {
        exclude group: 'org.apache.maven.shared', module: 'maven-filtering'
        exclude group: 'org.apache.maven.reporting', module: 'maven-reporting-api'
        exclude group: 'org.apache.maven.reporting', module: 'maven-reporting-impl'
    }

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

pluginBundle {
    website = 'https://trustin.github.io/sphinx-gradle-plugin/'
    vcsUrl = 'https://github.com/trustin/sphinx-gradle-plugin'
    plugins {
        sphinxPlugin {
            id = 'kr.motd.sphinx'
            displayName = "${project.name}"
            description = 'Sphinx site generation plugin for Gradle'
            tags = ['documentation', 'generator', 'python', 'site', 'site-generator', 'sphinx']
        }
    }
    mavenCoordinates {
        groupId = "${project.group}"
        artifactId = "${project.name}"
    }
}

task site(type: JavaExec, dependsOn: tasks.check) {
    inputs.dir "${project.projectDir}/src/sphinx"
    outputs.dir "${project.buildDir}/site"
    classpath sourceSets.test.runtimeClasspath
    main 'kr.motd.gradle.sphinx.SiteMain'
    args "${project.projectDir}", "${gradle.gradleUserHomeDir}"
}
